#version 460

#extension GL_EXT_ray_query : enable
#extension GL_EXT_ray_flags_primitive_culling : enable

#extension GL_GOOGLE_include_directive : require

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(set = 0, binding = 0) uniform U_GuiInput {
    float debug_float;
    uint debug_bool; // bool
    vec3 sun_dir;
    float sun_size;
    vec3 sun_color;
}
gui_input;
layout(set = 0, binding = 1) uniform U_CameraInfo {
    vec4 camera_pos; // w is padding.
    mat4 view_mat;
    mat4 view_mat_inv;
    mat4 proj_mat;
    mat4 proj_mat_inv;
    mat4 view_proj_mat;
    mat4 view_proj_mat_inv;
}
camera_info;
layout(set = 0, binding = 2) uniform U_EnvInfo { uint time_stamp; }
env_info;
#include "../include/contree_node.glsl"
layout(set = 0, binding = 3) readonly buffer B_ContreeNodeData { ContreeNode data[]; }
contree_node_data;
layout(set = 0, binding = 4) readonly buffer B_ContreeLeafData { uint data[]; }
contree_leaf_data;
layout(set = 0, binding = 5, rg32ui) readonly uniform uimage3D scene_tex;

layout(set = 1, binding = 0) writeonly uniform image2D result_image;

layout(set = 2, binding = 0) uniform accelerationStructureEXT tlas;

#include "../include/contree_marching.glsl"
#include "../include/core/definitions.glsl"
#include "../include/core/packer.glsl"
#include "../include/core/shader_clock.glsl"
#include "../include/core/viridis.glsl"
#include "../include/dda_scene_marching_result.glsl"
#include "../include/ray.glsl"
#include "../include/skylight.glsl"
#include "../include/voxel_color.glsl"

bool scene_hit(inout DdaSceneMarchingResult dda_res, vec3 o, vec3 d, ivec3 map_pos,
               uvec4 scene_tex_read) {
    // see update_scene_tex.comp for the encoding part
    if (scene_tex_read.x == 0) {
        return false;
    }
    scene_tex_read -= 1;

    ContreeMarchingResult contree_res =
        contree_marching(o, d, map_pos, vec3(1.0), false, scene_tex_read.x, scene_tex_read.y);
    if (contree_res.is_hit) {
        dda_res.is_hit     = true;
        dda_res.pos        = contree_res.pos;
        dda_res.center_pos = contree_res.center_pos;
        dda_res.normal     = contree_res.normal;
        dda_res.voxel_data = contree_res.voxel_data;
        return true;
    }
    return false;
}

#define SCENE_TEX_NAME scene_tex
#include "../include/dda_scene_marching.glsl"

///

vec3 get_shadow_ray_color(Ray ray) {
    DdaSceneMarchingResult res_shadow_ray;
    res_shadow_ray = dda_scene_marching(ray.origin, ray.direction, ray.inv_direction);
    if (res_shadow_ray.is_hit) {
        return vec3(0.0);
    }
    return get_sky_color(ray.direction);
}

void main() {
    ivec2 uvi      = ivec2(gl_GlobalInvocationID.xy);
    ivec2 img_size = imageSize(result_image);
    if (any(greaterThanEqual(uvi, img_size))) {
        return;
    }

    vec2 screen_uv = (vec2(gl_GlobalInvocationID.xy) + vec2(0.5)) / vec2(img_size);
    Ray ray        = ray_gen(camera_info.camera_pos.xyz, screen_uv);

    vec3 vis = vec3(0.0);

    DdaSceneMarchingResult res_primary_ray =
        dda_scene_marching(ray.origin, ray.direction, ray.inv_direction);

    if (res_primary_ray.is_hit) {
        // unpack normal from the packed voxel data (bits 8..28)
        vec3 normal = unpack_normal_v2((res_primary_ray.voxel_data & 0x1FFFFF00u) >> 8);

        uint voxel_type = res_primary_ray.voxel_data & 0xFFu;
        vec3 albedo     = voxel_color_by_type_unorm(voxel_type);

        vec3 shadow_ray_col = vec3(0.0);

        Ray shadow_ray;
        shadow_ray.direction     = gui_input.sun_dir;
        shadow_ray.inv_direction = vec3(1.0) / shadow_ray.direction;

        // gives more accurate shadow
        vec2 t            = slabs(vec3(-1.0 / 512.0), vec3(1.0 / 512.0), vec3(0.0), 1.0 / normal);
        vec3 march_extent = normal * (t.y + 0.004);

        shadow_ray.origin = res_primary_ray.center_pos + march_extent;

        if (dot(shadow_ray.direction, normal) > 0.0) {
            shadow_ray_col             = get_shadow_ray_color(shadow_ray);
            const float shadow_ray_pdf = 1.0 / (0.01 * PI);
            shadow_ray_col *= dot(shadow_ray.direction, normal) / shadow_ray_pdf;
        }

        vec3 brdf = albedo * INV_PI;
        vis       = gui_input.sun_color * brdf * shadow_ray_col;

        // if (gui_input.debug_bool != 0) {
        //     vis = res_primary_ray.center_pos;
        // } else {
        //     vis = res_primary_ray.pos;
        // }
    } else {
        vis = get_sky_color(ray.direction);
    }

    imageStore(result_image, uvi, vec4(vis, 1.0));
}

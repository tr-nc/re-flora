#version 460

#extension GL_EXT_ray_query : enable
#extension GL_EXT_ray_flags_primitive_culling : enable

#extension GL_GOOGLE_include_directive : require

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(set = 0, binding = 0) uniform U_GuiInput {
    float debug_float;
    uint debug_bool; // bool
    vec3 sun_dir;
    float sun_size;
    vec3 sun_color;
}
gui_input;
layout(set = 0, binding = 1) uniform U_CameraInfo {
    vec4 camera_pos; // w is padding.
    mat4 view_mat;
    mat4 view_mat_inv;
    mat4 proj_mat;
    mat4 proj_mat_inv;
    mat4 view_proj_mat;
    mat4 view_proj_mat_inv;
}
camera_info;
layout(set = 0, binding = 2) uniform U_EnvInfo { uint frame_serial_idx; }
env_info;
#include "../include/contree_node.glsl"
layout(set = 0, binding = 3) readonly buffer B_ContreeNodeData { ContreeNode data[]; }
contree_node_data;
layout(set = 0, binding = 4) readonly buffer B_ContreeLeafData { uint data[]; }
contree_leaf_data;
layout(set = 0, binding = 5, rg32ui) readonly uniform uimage3D scene_tex;
layout(set = 0, binding = 6, r8) readonly uniform image2DArray scalar_bn;
layout(set = 0, binding = 7, rg8) readonly uniform image2DArray unit_vec2_bn;
layout(set = 0, binding = 8, rgba8) readonly uniform image2DArray unit_vec3_bn;
layout(set = 0, binding = 9, rgba8) readonly uniform image2DArray weighted_cosine_bn;
layout(set = 0, binding = 10, rgba8) readonly uniform image2DArray fast_weighted_cosine_bn;

layout(set = 1, binding = 0) writeonly uniform image2D result_image;

layout(set = 2, binding = 0) uniform accelerationStructureEXT tlas;

#include "../include/contree_marching.glsl"
#include "../include/core/definitions.glsl"
#include "../include/core/packer.glsl"
#include "../include/core/shader_clock.glsl"
#include "../include/core/viridis.glsl"
#include "../include/marching_result.glsl"
#include "../include/ray.glsl"
#include "../include/skylight.glsl"
#include "../include/voxel_color.glsl"

bool scene_hit(inout MarchingResult o_res, vec3 o, vec3 d, ivec3 map_pos, uvec4 scene_tex_read) {
    // see update_scene_tex.comp for encoding part
    if (scene_tex_read.x == 0) {
        return false;
    }
    scene_tex_read -= 1;

    ContreeMarchingResult contree_res =
        contree_marching(o, d, map_pos, vec3(1.0), false, scene_tex_read.x, scene_tex_read.y);
    if (contree_res.is_hit) {
        o_res.is_hit     = true;
        o_res.pos        = contree_res.pos;
        o_res.center_pos = contree_res.center_pos;
        o_res.normal     = unpack_normal_v2((contree_res.voxel_data & 0x1FFFFF00u) >> 8);
        o_res.voxel_type = contree_res.voxel_data & 0xFFu;
        return true;
    }
    return false;
}

#define SCENE_TEX_NAME scene_tex
#include "../include/dda_scene_marching.glsl"

///

MarchingResult combined_scene_marching(Ray ray, bool is_primary_ray) {
    MarchingResult res;
    res = dda_scene_marching(ray.origin, ray.direction, ray.inv_direction);
    if (!is_primary_ray) {
        return res; // for shadow rays, we don't need to do ray query
    }

    rayQueryEXT rayQuery;
    rayQueryInitializeEXT(rayQuery, tlas,
                          gl_RayFlagsOpaqueEXT, // skip “transparent” triangles
                          0xFF,                 // instance mask
                          ray.origin,
                          0.001, // bias off your surface
                          ray.direction, 10000.0);

    // march through *every* candidate
    while (rayQueryProceedEXT(rayQuery)) {
        if (rayQueryGetIntersectionTypeEXT(rayQuery, false) ==
            gl_RayQueryCandidateIntersectionTriangleEXT) {
            // “commit” it; this tells the driver “yes, I want
            // you to consider this for the nearest‐hit test”
            rayQueryConfirmIntersectionEXT(rayQuery);
        }
    }
    // after traversal, ask whether we actually committed a hit
    bool hit_grass = (rayQueryGetIntersectionTypeEXT(rayQuery, true) ==
                      gl_RayQueryCommittedIntersectionTriangleEXT);
    float t_grass  = rayQueryGetIntersectionTEXT(rayQuery, true);

    if (hit_grass && t_grass < res.t) {
        // we hit the grass, so we override the result
        res.is_hit = true;
        res.pos    = ray.origin + ray.direction * t_grass;
        // FIXME:
        res.center_pos = res.pos;
        res.normal     = vec3(0.0, 1.0, 0.0);
        res.voxel_type = VOXEL_TYPE_GRASS;
    }

    return res;
}

const ivec3 BN_NOISE_TEX_SIZE = ivec3(128, 128, 64);
ivec3 get_seed() { return ivec3(gl_GlobalInvocationID.xy % 128, env_info.frame_serial_idx % 64); }

// range (0, 1)
float random_float_bn(ivec3 seed) { return imageLoad(scalar_bn, seed).r; }

// range (-1, 1) in 2 components
vec2 random_unit_vec2_bn(ivec3 seed) { return imageLoad(unit_vec2_bn, seed).rg * 2.0 - 1.0; }

// range (-1, 1) in 3 components
vec3 random_unit_vec3_bn(ivec3 seed) { return imageLoad(unit_vec3_bn, seed).rgb * 2.0 - 1.0; }

// range (-1, 1) in x and y, range (0, 1) in z
vec3 random_weighted_cosine_bn(ivec3 seed) {
    return imageLoad(fast_weighted_cosine_bn, seed).rgb * 2.0 - 1.0;
}

mat3 make_tbn(vec3 normal) {
    vec3 up = abs(normal.z) < 0.999 ? vec3(0, 0, 1) : vec3(1, 0, 0);
    vec3 t  = normalize(cross(up, normal));
    vec3 b  = cross(normal, t);
    return mat3(t, b, normal);
}

vec3 sample_diffuse_bounce(ivec3 seed, vec3 normal) {
    vec3 weighted_cosine_bn = random_weighted_cosine_bn(seed);
    mat3 tbn                = make_tbn(normal);
    return tbn * weighted_cosine_bn;
}

vec3 sample_sun_disk(ivec3 seed, float sun_size, vec3 sun_dir) {
    vec2 random_xy = random_weighted_cosine_bn(seed).xy;
    random_xy *= sun_size;
    mat3 tbn = make_tbn(sun_dir);
    return normalize(sun_dir + tbn * vec3(random_xy.xy, 0.0));
}

///

/// If the ray hits the scene, no light, otherwise sunlight
vec3 get_shadow_ray_color(Ray ray) {
    MarchingResult res_shadow_ray;
    res_shadow_ray = combined_scene_marching(ray, true);
    return res_shadow_ray.is_hit ? vec3(0.0) : gui_input.sun_color * 0.01;
}

vec3 get_next_tracing_pos(vec3 voxel_center_pos, vec3 voxel_normal) {
    // gives more accurate shadow
    vec2 t                = slabs(vec3(-1.0), vec3(1.0), vec3(0.0), 1.0 / voxel_normal);
    float half_voxel_size = 1.0 / 512.0; // assuming the voxel size is 1/512
    // 0.005 is experimental to maintain a good shadow quality
    vec3 march_extent = voxel_normal * (t.y * half_voxel_size + 0.005);
    return voxel_center_pos + march_extent;
}

vec3 get_lambertian_brdf(vec3 albedo) { return albedo * INV_PI; }

void parse_trace_result(out vec3 albedo, out vec3 next_tracing_pos, MarchingResult res) {
    albedo           = voxel_color_by_type_unorm(res.voxel_type);
    next_tracing_pos = get_next_tracing_pos(res.center_pos, res.normal);
}

vec3 get_pixel_color(Ray ray, ivec3 seed) {
    MarchingResult res_primary_ray = combined_scene_marching(ray, true);

    if (!res_primary_ray.is_hit) {
        // render skybox
        return get_sky_color(ray.direction);
    }
    vec3 albedo, next_tracing_pos;
    parse_trace_result(albedo, next_tracing_pos, res_primary_ray);

    vec3 direct_color = vec3(0.0);

    Ray shadow_ray;
    shadow_ray.direction     = sample_sun_disk(seed, gui_input.sun_size, gui_input.sun_dir);
    shadow_ray.inv_direction = vec3(1.0) / shadow_ray.direction;
    shadow_ray.origin        = next_tracing_pos;

    float cos_i = dot(shadow_ray.direction, res_primary_ray.normal);
    if (cos_i > 0.0) {
        // we ignored the pdf and brdf calc here, because the sun's luminance is experimental
        direct_color = get_shadow_ray_color(shadow_ray) * cos_i * albedo;
    }

    vec3 indirect_color = vec3(0.0);

    Ray indirect_ray;
    indirect_ray.direction     = sample_diffuse_bounce(seed, res_primary_ray.normal);
    indirect_ray.inv_direction = vec3(1.0) / indirect_ray.direction;
    indirect_ray.origin        = next_tracing_pos;

    MarchingResult res_indirect_ray = combined_scene_marching(indirect_ray,true);
    if (!res_indirect_ray.is_hit) {
        indirect_color = get_sky_color(indirect_ray.direction);
    } else {
        vec3 sample_albedo, sample_next_tracing_pos;
        parse_trace_result(sample_albedo, sample_next_tracing_pos, res_indirect_ray);
        // only change the origin of the ray, reuse the direction for performance
        shadow_ray.origin = sample_next_tracing_pos;

        float cos_i = dot(shadow_ray.direction, res_indirect_ray.normal);
        if (cos_i > 0.0) {
            indirect_color = get_shadow_ray_color(shadow_ray) * cos_i * sample_albedo;
        }
    }
    // see README for the brdf, pdf explanation
    indirect_color *= albedo;

    return direct_color + indirect_color;
}

void main() {
    ivec2 uvi      = ivec2(gl_GlobalInvocationID.xy);
    ivec2 img_size = imageSize(result_image);
    if (any(greaterThanEqual(uvi, img_size))) {
        return;
    }

    ivec3 seed = get_seed();

    vec2 screen_uv = (vec2(gl_GlobalInvocationID.xy) + vec2(0.5)) / vec2(img_size);
    Ray ray        = ray_gen(camera_info.camera_pos.xyz, screen_uv);

    vec3 vis = vec3(0.0);

    vis = get_pixel_color(ray, seed);
    imageStore(result_image, uvi, vec4(vis, 1.0));
}

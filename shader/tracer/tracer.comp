#version 460

#extension GL_EXT_ray_query : enable
#extension GL_EXT_ray_flags_primitive_culling : enable

#extension GL_GOOGLE_include_directive : require

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(set = 0, binding = 0) uniform U_GuiInput { float debug_float; }
gui_input;
layout(set = 0, binding = 1) uniform U_CameraInfo {
    vec4 camera_pos; // w is padding.
    mat4 view_mat;
    mat4 view_mat_inv;
    mat4 proj_mat;
    mat4 proj_mat_inv;
    mat4 view_proj_mat;
    mat4 view_proj_mat_inv;
}
camera_info;
layout(set = 0, binding = 2) uniform U_EnvInfo { uint time_stamp; }
env_info;
layout(set = 0, binding = 3) uniform accelerationStructureEXT tlas;
layout(set = 0, binding = 4) writeonly uniform image2D result_image;

#include "../include/core/packer.glsl"
#include "../include/core/shader_time.glsl"
#include "../include/core/viridis.glsl"
#include "../include/ray.glsl"
#include "../include/voxel_color.glsl"
#include "../include/voxel_type.glsl"

// intersects if: tmin < tmax && tmax > 0.0
vec2 slabs(vec3 p0, vec3 p1, vec3 o, vec3 inv_d) {
    vec3 t0 = (p0 - o) * inv_d;
    vec3 t1 = (p1 - o) * inv_d;

    vec3 temp = t0;
    t0 = min(temp, t1), t1 = max(temp, t1);

    float tmin = max(max(t0.x, t0.y), t0.z);
    float tmax = min(min(t1.x, t1.y), t1.z);

    return vec2(tmin, tmax);
}

void get_voxel_type_normal(out uint o_voxel_type, out vec3 o_normal, out bool o_is_normal_valid,
                           uint compressed_voxel_type_and_normal) {
    o_voxel_type      = compressed_voxel_type_and_normal & 0xFF;
    o_normal          = unpack_normal_v2((compressed_voxel_type_and_normal & 0x1FFFFF00u) >> 8);
    o_is_normal_valid = (compressed_voxel_type_and_normal & 0x20000000u) != 0;
}

bool _in_chunk_range(ivec3 pos, ivec3 visible_chunk_dim) {
    return all(greaterThanEqual(pos, ivec3(0))) && all(lessThan(pos, visible_chunk_dim));
}

bool _has_chunk(ivec3 chunk_idx) { return true; }

// this function if used for continuous raymarching, where we need to save the last hit chunk
bool dda_marching_with_save(out ivec3 o_chunk_idx, inout ivec3 map_pos, inout vec3 side_dist,
                            inout bool entered_visible_region, inout uint it,
                            ivec3 visible_chunk_dim, vec3 delta_dist, ivec3 ray_step) {
    bvec3 mask;
    while (it++ < 1024) {
        mask = lessThanEqual(side_dist.xyz, min(side_dist.yzx, side_dist.zxy));
        side_dist += vec3(mask) * delta_dist;

        o_chunk_idx = map_pos;
        map_pos += ivec3(vec3(mask)) * ray_step;

        if (_in_chunk_range(o_chunk_idx, visible_chunk_dim)) {
            entered_visible_region = true;
            if (_has_chunk(o_chunk_idx)) {
                return true;
            }
        }
        // went outside the outer bounding box
        else if (entered_visible_region) {
            return false;
        }
    }
    return false;
}

uint dda_svo_marching(ivec3 visible_chunk_dim, vec3 o, vec3 d, vec3 inv_d) {
    vec3 min_bound = vec3(0.0);
    vec3 max_bound = vec3(visible_chunk_dim);

    vec2 t = slabs(min_bound, max_bound, o, inv_d);
    if (t.x > t.y || t.y < 0.0) {
        return 0;
    }

    vec3 pos = o + (t.x + 1e-4) * d;

    float t_max     = 0;
    uint iter_count = 0;
    while (iter_count++ < 256) {
        vec3 cell_min  = floor(pos);
        vec3 cell_size = vec3(1.0);

        vec3 side_pos  = cell_min + step(0.0, d) * cell_size;
        vec3 side_dist = (side_pos - o) * inv_d;

        t_max = min(min(side_dist.x, side_dist.y), side_dist.z) + 1e-4;

        pos = o + t_max * d;

        if (t_max > t.y) {
            break;
        }
    }
    return iter_count;
}

void main() {
    ivec2 uvi      = ivec2(gl_GlobalInvocationID.xy);
    ivec2 img_size = imageSize(result_image);
    if (any(greaterThanEqual(uvi, img_size))) {
        return;
    }

    vec2 screen_uv = (vec2(gl_GlobalInvocationID.xy) + vec2(0.5)) / vec2(img_size);
    Ray ray        = ray_gen(camera_info.camera_pos.xyz, screen_uv);

    uint hit_count = 0;

    // in benchmark, dda is much faster than hardware rtx:
    // 227 fps for full cube
    // 390 fps for half cube
    // 500 fps for a single blas
    // 3000 fps for dda slab

    // dda experiments
    // 1750 fps for 200x8x200
    // 1850 fps when beam opti
    // 3000 fps

    // hardware rtx
    // rayQueryEXT rq;
    // rayQueryInitializeEXT(rq, tlas, gl_RayFlagsNoneEXT, 0xFF, ray.origin, 0.001, ray.direction,
    //                       1e4);
    // while (rayQueryProceedEXT(rq)) {
    //     if (rayQueryGetIntersectionTypeEXT(rq, /*committed=*/false) ==
    //         gl_RayQueryCandidateIntersectionTriangleEXT) {
    //         hit_count++;
    //     }
    // }

    uvec2 start_time = get_current_time();

    // slab dda
    hit_count = dda_svo_marching(ivec3(200, 8, 200), ray.origin, ray.direction, ray.inv_direction);
    // hit_count = dda_svo_marching(ivec3(4, 4, 4), ray.origin, ray.direction, ray.inv_direction);

    uvec2 end_time = get_current_time();

    float delta_time = get_delta_time(start_time, end_time);
    vec3 time_vis    = inferno_quintic(1.0 - exp(-float(delta_time)));
    vec3 vis         = inferno_quintic(1.0 - exp(-float(hit_count) * 0.1));
    // vis              = mix(vis, time_vis, 0.5);

    imageStore(result_image, uvi, vec4(vis, 1.0));
}

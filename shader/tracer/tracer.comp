#version 450

#extension GL_GOOGLE_include_directive : require
// #extension GL_EXT_shader_image_load_formatted : require

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(set = 0, binding = 0) writeonly uniform image2D result_image;
layout(set = 0, binding = 1) uniform GuiInput { float debug_float; }
gui_input;
layout(set = 0, binding = 2) uniform CameraInfo {
    vec4 camera_pos; // w is padding.
    mat4 view_mat;
    mat4 view_mat_inv;
    mat4 proj_mat;
    mat4 proj_mat_inv;
    mat4 view_proj_mat;
    mat4 view_proj_mat_inv;
}
camera_info;
layout(set = 0, binding = 3) buffer B_OctreeData { uint data[]; }
octree_data;
layout(set = 0, binding = 4, r8ui) readonly uniform uimage3D octree_offset_atlas_tex;

#include "../include/core/projection.glsl"
#include "../include/svo_marching.glsl"

struct Ray {
    vec3 origin;
    vec3 direction;
    vec3 inv_direction;
};

Ray ray_gen(vec3 camera_pos, vec2 screen_uv) {
    Ray ray;
    ray.origin        = camera_pos;
    ray.direction     = normalize(project_screen_uv_to_world_cam_far_point(screen_uv) - camera_pos);
    ray.inv_direction = 1.0 / ray.direction;
    return ray;
}

void main() {
    ivec2 uvi      = ivec2(gl_GlobalInvocationID.xy);
    ivec2 img_size = imageSize(result_image);
    if (any(greaterThanEqual(uvi, img_size))) {
        return;
    }

    vec2 screen_uv = (vec2(gl_GlobalInvocationID.xy) + vec2(0.5)) / vec2(img_size);
    Ray ray        = ray_gen(camera_info.camera_pos.xyz, screen_uv);

    SvoMarchingResult res = svo_marching(ray.origin, ray.direction, 0);

    vec3 voxel_color = vec3(0.0);
    if (res.is_hit) {
        voxel_color = res.normal * 0.5 + 0.5;
    }

    vec4 out_color = vec4(0.0);
    out_color      = vec4(voxel_color, 1.0);

    imageStore(result_image, uvi, out_color);
}

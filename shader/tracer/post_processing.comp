/// For upscaling the rendering output to the screen and applying postprocessing effects

#version 460

#extension GL_GOOGLE_include_directive : require

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(set = 0, binding = 0) uniform U_PostProcessingInfo { float scaling_factor; }
post_processing_info;
layout(set = 0, binding = 1, rgba8) uniform readonly image2D gfx_output_tex;
layout(set = 0, binding = 2, r32f) uniform readonly image2D gfx_depth_tex;
layout(set = 0, binding = 3, rgba8) uniform readonly image2D compute_output_tex;
layout(set = 0, binding = 4, r32f) uniform readonly image2D compute_depth_tex;
layout(set = 0, binding = 5, rgba8) uniform writeonly image2D screen_output_tex;

void main() {
    ivec2 uvi = ivec2(gl_GlobalInvocationID.xy);
    if (any(greaterThanEqual(uvi, imageSize(screen_output_tex)))) {
        return;
    }

    ivec2 mapped_uvi = ivec2(vec2(uvi) * post_processing_info.scaling_factor);

    float gfx_depth_01     = imageLoad(gfx_depth_tex, mapped_uvi).r;
    float compute_depth_01 = imageLoad(compute_depth_tex, mapped_uvi).r;
    if (gfx_depth_01 > compute_depth_01) {
        vec4 compute_color = imageLoad(compute_output_tex, mapped_uvi);
        imageStore(screen_output_tex, uvi, compute_color);
        return;
    }
    vec4 gfx_color = imageLoad(gfx_output_tex, mapped_uvi);
    imageStore(screen_output_tex, uvi, gfx_color);
}

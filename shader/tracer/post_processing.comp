/// For upscaling the rendering output to the screen and applying postprocessing effects

#version 460

#extension GL_GOOGLE_include_directive : require

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(set = 0, binding = 0) uniform U_PostProcessingInfo { float scaling_factor; }
post_processing_info;
layout(set = 0, binding = 1, rgba8) uniform readonly image2D gfx_output_tex;
layout(set = 0, binding = 2, r32f) uniform readonly image2D gfx_depth_tex;
layout(set = 0, binding = 3, rgba8) uniform readonly image2D compute_output_tex;
layout(set = 0, binding = 4, r32f) uniform readonly image2D compute_depth_tex;
layout(set = 0, binding = 5, rgba8) uniform writeonly image2D screen_output_tex;
layout(set = 0, binding = 6, rgba32f) uniform readonly image2D god_ray_output_tex;

#define DEBUGGING_TEXTURE_NAME god_ray_output_tex
#define OUTPUT_TEXTURE_NAME screen_output_tex
#include "../include/core/debug.glsl"

#include "../include/core/dither.glsl"

vec3 combine_colors(float gfx_depth_01, float compute_depth_01, vec4 gfx_color,
                    vec4 compute_color) {
    if (gfx_depth_01 > compute_depth_01) {
        return compute_color.rgb;
    }
    return gfx_color.rgb;
}

void main() {
    ivec2 uvi = ivec2(gl_GlobalInvocationID.xy);
    if (any(greaterThanEqual(uvi, imageSize(screen_output_tex)))) {
        return;
    }

    ivec2 mapped_uvi = ivec2(vec2(uvi) * post_processing_info.scaling_factor);

    float gfx_depth_01     = imageLoad(gfx_depth_tex, mapped_uvi).r;
    float compute_depth_01 = imageLoad(compute_depth_tex, mapped_uvi).r;
    vec4 gfx_color         = imageLoad(gfx_output_tex, mapped_uvi);
    vec4 compute_color     = imageLoad(compute_output_tex, mapped_uvi);
    vec3 final_color = combine_colors(gfx_depth_01, compute_depth_01, gfx_color, compute_color);

    ivec2 god_ray_tex_size      = imageSize(god_ray_output_tex);
    vec2 god_ray_scaling_factor = vec2(god_ray_tex_size) / vec2(imageSize(screen_output_tex));
    ivec2 god_ray_mapped_uvi    = ivec2(vec2(uvi) * god_ray_scaling_factor);
    float dithering             = bayer_16(vec2(uvi) * 0.5) - 0.5;
    float god_ray_weight        = imageLoad(god_ray_output_tex, god_ray_mapped_uvi).r;
    god_ray_weight += dithering;

    god_ray_weight = god_ray_weight < 0.5 ? 0.0 : 1.0;

    // 255, 178, 0
    vec3 dot_color     = vec3(1.0, 0.698, 0.0);
    vec4 god_ray_color = vec4(dot_color, 0.6 * god_ray_weight);

    // overlap the god ray color with the final color
    final_color = mix(final_color, god_ray_color.rgb, god_ray_color.a);

    imageStore(screen_output_tex, uvi, vec4(final_color, 1.0));
}

#version 460

#extension GL_GOOGLE_include_directive : require

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(set = 0, binding = 0) uniform U_GuiInput {
    float debug_float;
    uint debug_bool;
    uint debug_uint;
}
gui_input;
layout(set = 0, binding = 1) uniform U_SunInfo {
    vec3 sun_dir;
    float sun_size;
    vec3 sun_color;
    float sun_luminance;
}
sun_info;
layout(set = 0, binding = 2) uniform U_SkyInfo {
    vec3 debug_color_1;
    vec3 debug_color_2;
    uint use_debug_sky_colors;
}
sky_info;
layout(set = 0, binding = 3) uniform U_CameraInfo {
    vec4 pos;
    mat4 view_mat;
    mat4 view_mat_inv;
    mat4 proj_mat;
    mat4 proj_mat_inv;
    mat4 view_proj_mat;
    mat4 view_proj_mat_inv;
}
camera_info;
layout(set = 0, binding = 4) uniform U_EnvInfo { uint frame_serial_idx; }
env_info;
layout(set = 0, binding = 5) uniform U_StarlightInfo {
    int iterations;
    float formuparam;
    int volsteps;
    float stepsize;
    float zoom;
    float tile;
    float speed;
    float brightness;
    float darkmatter;
    float distfading;
    float saturation;
}
starlight_info;
layout(set = 0, binding = 6, rgba8) uniform readonly image2D gfx_output_tex;
layout(set = 0, binding = 7, r32f) uniform readonly image2D gfx_depth_tex;
layout(set = 0, binding = 8, r11f_g11f_b10f) uniform readonly image2D denoiser_spatial_pong_tex;
layout(set = 0, binding = 9, r32f) uniform readonly image2D compute_depth_tex;
layout(set = 0, binding = 10, r32f) uniform readonly image2D god_ray_output_tex;
layout(set = 0, binding = 11, r11f_g11f_b10f) uniform writeonly image2D composited_tex;
layout(set = 0, binding = 12, r8) uniform readonly image2D star_noise_tex;

#include "../include/core/color.glsl"
#include "../include/core/dither.glsl"
#include "../include/ray.glsl"
#include "../include/skylight.glsl"
#include "../include/starlight.glsl"

vec3 combine_colors(float gfx_depth_01, float compute_depth_01, vec3 gfx_color,
                    vec3 denoiser_output_color, vec2 screen_uv, ivec2 uvi) {
    if (gfx_depth_01 == 1.0 && compute_depth_01 == 1.0) {
        Ray ray        = ray_gen(screen_uv, camera_info.view_proj_mat_inv);
        vec3 sky_color = get_sky_color_with_sun(ray.direction, sun_info.sun_dir, sun_info.sun_color,
                                                sun_info.sun_luminance, sun_info.sun_size,
                                                sky_info.use_debug_sky_colors,
                                                sky_info.debug_color_1, sky_info.debug_color_2);
        // Star visibility thresholds based on sun altitude
        const float star_cutoff_altitude =
            0.1; // Stars completely hidden when sun is above this altitude
        const float star_fade_altitude =
            -0.2; // Stars fully visible when sun is below this altitude

        float sun_altitude = sun_info.sun_dir.y;

        // Skip expensive star calculation when sun is too high
        if (sun_altitude > star_cutoff_altitude) {
            return sky_color;
        }

        // Calculate star light contribution
        StarlightInfo info = StarlightInfo(
            starlight_info.iterations, starlight_info.formuparam, starlight_info.volsteps,
            starlight_info.stepsize, starlight_info.zoom, starlight_info.tile, starlight_info.speed,
            starlight_info.brightness, starlight_info.darkmatter, starlight_info.distfading,
            starlight_info.saturation);
        vec3 star_color = get_starlight_color(ray.direction, info, sun_info.sun_dir);

        // Fade stars based on sun altitude
        float star_visibility = smoothstep(star_cutoff_altitude, star_fade_altitude, sun_altitude);
        return mix(sky_color, star_color, star_visibility);
    }

    if (gfx_depth_01 > compute_depth_01) {
        return denoiser_output_color;
    }
    return gfx_color;
}

void main() {
    ivec2 uvi = ivec2(gl_GlobalInvocationID.xy);
    if (any(greaterThanEqual(uvi, imageSize(composited_tex)))) {
        return;
    }

    vec2 img_size  = vec2(imageSize(composited_tex));
    vec2 screen_uv = (vec2(gl_GlobalInvocationID.xy) + vec2(0.5)) / img_size;

    float gfx_depth_01             = imageLoad(gfx_depth_tex, uvi).r;
    float compute_depth_01         = imageLoad(compute_depth_tex, uvi).r;
    vec3 gfx_color                 = imageLoad(gfx_output_tex, uvi).rgb;
    vec3 denoiser_spatial_pong_tex = imageLoad(denoiser_spatial_pong_tex, uvi).rgb;
    vec3 final_color               = combine_colors(gfx_depth_01, compute_depth_01, gfx_color,
                                                    denoiser_spatial_pong_tex, screen_uv, uvi);

    float god_ray_weight = imageLoad(god_ray_output_tex, uvi).r;

    vec3 dot_color     = srgb_to_linear(vec3(1.0, 0.945, 0.0));
    vec4 god_ray_color = vec4(dot_color, god_ray_weight);
    // final_color = mix(final_color, god_ray_color.rgb, god_ray_color.a);

    imageStore(composited_tex, uvi, vec4(final_color, 1.0));
}

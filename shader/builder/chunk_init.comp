//! Inits the chunk image with artificial noise.
#version 450
#extension GL_GOOGLE_include_directive : require

layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

layout(set = 0, binding = 0) readonly uniform U_ChunkBuildInfo {
    uvec3 chunk_res;
    ivec3 chunk_pos;
}
chunk_build_info;
layout(set = 1, binding = 0, r8ui) writeonly uniform uimage3D raw_voxels_img;

#include "../include/core/inoise.glsl"
#include "../include/voxel_types.glsl"

float compute_noise(vec3 p) {
    float total       = 0.0;
    float amplitude   = 0.5;
    float frequency   = 2.0;
    float persistence = 0.3;
    float lacunarity  = 2.2;
    int octaves       = 5;

    for (int i = 0; i < octaves; i++) {
        vec4 noise = noised(p * frequency);
        total += amplitude * (noise.x + 0.5);
        amplitude *= persistence;
        frequency *= lacunarity;
    }
    return total;
}

uint get_voxel_type(float weight) {
    if (weight < 0.0) {
        return VOXEL_TYPE_EMPTY;
    }
    return VOXEL_TYPE_DIRT;
}

void main() {
    ivec3 uvi = ivec3(gl_GlobalInvocationID);
    if (any(greaterThanEqual(uvi, ivec3(chunk_build_info.chunk_res)))) {
        return;
    }

    vec3 local_voxel_pos = (vec3(uvi) - 0.5) / vec3(chunk_build_info.chunk_res);
    vec3 world_voxel_pos = local_voxel_pos + vec3(chunk_build_info.chunk_pos);

    float noise  = compute_noise(world_voxel_pos);
    float weight = noise - world_voxel_pos.y;

    uint voxel_type = get_voxel_type(weight);

    imageStore(raw_voxels_img, uvi, uvec4(voxel_type));
}

#version 450
#extension GL_GOOGLE_include_directive : require

layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

struct RoundCone {
    vec3 center_a;
    float radius_a;
    vec3 center_b;
    float radius_b;
};

struct BvhNode {
    vec3 aabb_min;
    vec3 aabb_max;
    uint left_child;
    uint right_child;
};

layout(set = 0, binding = 0) readonly uniform U_ChunkModifyInfo {
    uvec3 chunk_pos;
    uint fill_voxel_type;
    uint round_cone_len;
}
chunk_modify_info;

layout(set = 0, binding = 1) readonly buffer B_BvhNodes { BvhNode data[]; }
bvh_nodes;

layout(set = 0, binding = 2) readonly buffer B_RoundCones { RoundCone data[]; }
round_cones;

layout(set = 0, binding = 3, r8ui) writeonly uniform uimage3D raw_atlas;

#include "../../include/config.glsl"
#include "../../include/core/sdf.glsl"
#include "../../include/voxel_types.glsl"

vec3 center_position_of_voxel(ivec3 voxel_pos) { return vec3(voxel_pos) + vec3(0.5); }

void main() {
    ivec3 uvi = ivec3(gl_GlobalInvocationID);
    if (any(greaterThanEqual(uvi, ivec3(VOXEL_DIM)))) {
        return;
    }

    const ivec3 atlas_base_offset = ivec3(chunk_modify_info.chunk_pos) * VOXEL_DIM;
    const ivec3 world_voxel_pos   = uvi + atlas_base_offset;

    vec3 world_voxel_pos_f = center_position_of_voxel(world_voxel_pos);

    for (int i = 0; i < chunk_modify_info.round_cone_len; i++) {
        float dst = sd_round_cone(world_voxel_pos_f, round_cones.data[i].center_a,
                                  round_cones.data[i].center_b, round_cones.data[i].radius_a,
                                  round_cones.data[i].radius_b);
        if (dst < 0.0) {
            imageStore(raw_atlas, world_voxel_pos,
                       uvec4(chunk_modify_info.fill_voxel_type, 0, 0, 0));
            return;
        }
    }
}

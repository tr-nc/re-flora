#version 450
#extension GL_GOOGLE_include_directive : require

layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

struct RoundCone {
    vec3 center_a;
    float radius_a;
    vec3 center_b;
    float radius_b;
};

layout(set = 0, binding = 0) readonly uniform U_ChunkModifyInfo {
    uvec3 chunk_pos;
    uint fill_voxel_type;
}
chunk_modify_info;

#include "../../include/bvh.glsl"
layout(set = 0, binding = 1) readonly buffer B_BvhNodes { BvhNode data[]; }
bvh_nodes;
layout(set = 0, binding = 2) readonly buffer B_RoundCones { RoundCone data[]; }
round_cones;
layout(set = 0, binding = 3, r8ui) writeonly uniform uimage3D raw_atlas;

#include "../../include/config.glsl"
#include "../../include/core/aabb.glsl"
#include "../../include/core/sdf.glsl"
#include "../../include/voxel_type.glsl"

vec3 center_position_of_voxel(ivec3 voxel_pos) { return vec3(voxel_pos) + vec3(0.5); }

void main() {
    ivec3 uvi = ivec3(gl_GlobalInvocationID);
    if (any(greaterThanEqual(uvi, ivec3(VOXEL_DIM)))) {
        return;
    }

    const ivec3 atlas_base_offset = ivec3(chunk_modify_info.chunk_pos) * VOXEL_DIM;
    const ivec3 world_voxel_pos   = uvi + atlas_base_offset;
    vec3 world_voxel_pos_f        = center_position_of_voxel(world_voxel_pos);

    // --- BVH traversal stack ------------------------------------------------
    const int MAX_STACK = 64;
    uint stack[MAX_STACK];
    int sp = 0;

    // push root
    stack[sp++] = 0u;

    while (sp > 0) {
        uint node_idx = stack[--sp];
        BvhNode node  = bvh_nodes.data[node_idx];

        if (!in_aabb(world_voxel_pos_f, node.aabb_min, node.aabb_max)) {
            continue;
        }

        // here the leaf data is the index of the round cone in the round_cones buffer
        uint udata = fetch_data(node);

        if (is_leaf(node)) {
            uint ci      = udata;
            RoundCone rc = round_cones.data[ci];
            float dst    = sd_round_cone(world_voxel_pos_f, rc.center_a, rc.center_b, rc.radius_a,
                                         rc.radius_b);
            if (dst < 0.0) {
                imageStore(raw_atlas, world_voxel_pos, uvec4(VOXEL_TYPE_CHUNK, 0, 0, 0));
                return;
            }
        } else {
            stack[sp++] = udata + 1u;
            stack[sp++] = udata;
        }
    }
    // no hit â‡’ leave voxel alone
}

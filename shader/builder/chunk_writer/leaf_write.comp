#version 450
#extension GL_GOOGLE_include_directive : require

layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

layout(set = 0, binding = 0) readonly uniform U_LeafWriteInfo {
    vec3 leaf_color;
    uvec3 atlas_write_offset;
    uvec3 atlas_write_dim;
}
leaf_write_info;

layout(set = 0, binding = 1, r8ui) writeonly uniform uimage3D atlas;

#include "../../include/core/inoise.glsl"
#include "../../include/voxel_types.glsl"

float compute_noise(vec3 p) {
    // if all three are integer values, the noise is fixed to zero
    return noised(p * 0.9).x;
}

uint get_voxel_type(float weight) {
    if (weight < 0.5) {
        return VOXEL_TYPE_EMPTY;
    }
    return VOXEL_TYPE_LEAF;
}

void main() {
    ivec3 uvi = ivec3(gl_GlobalInvocationID);
    if (any(greaterThanEqual(uvi, ivec3(leaf_write_info.atlas_write_dim)))) {
        return;
    }

    float noise = compute_noise(vec3(uvi));
    // float noise     = noised(vec3(uvi)).x + 0.5;
    uint voxel_type = get_voxel_type(noise);

    ivec3 atlas_base_offset = ivec3(leaf_write_info.atlas_write_offset);
    imageStore(atlas, atlas_base_offset + uvi, uvec4(voxel_type, 0, 0, 0));
}

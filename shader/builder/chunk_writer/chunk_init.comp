#version 450
#extension GL_GOOGLE_include_directive : require

layout(local_size_x = 4, local_size_y = 4, local_size_z = 4) in;

layout(set = 0, binding = 0) readonly uniform U_RegionInfo {
    uvec3 offset;
    uvec3 dim;
}
region_info;

layout(set = 0, binding = 1, r8ui) writeonly uniform uimage3D raw_atlas;

#include "../../include/config.glsl"
#include "../../include/core/inoise.glsl"
#include "../../include/voxel_type.glsl"

float compute_noise(vec3 p) {
    float total       = 0.0;
    float amplitude   = 0.5;
    float frequency   = 2.0;
    float persistence = 0.3;
    float lacunarity  = 2.2;
    int octaves       = 5;

    for (int i = 0; i < octaves; i++) {
        float noise = noised(p * frequency).x;
        total += amplitude * (noise + 0.5);
        amplitude *= persistence;
        frequency *= lacunarity;
    }
    return total;
}

uint get_voxel_type(float weight) {
    if (weight < 0.0) {
        return VOXEL_TYPE_EMPTY;
    }
    return VOXEL_TYPE_GRASS_LAND;
}

void main() {
    ivec3 uvi = ivec3(gl_GlobalInvocationID);
    if (any(greaterThanEqual(uvi, ivec3(region_info.dim)))) {
        return;
    }

    ivec3 atlas_uvi         = ivec3(region_info.offset + uvi);
    const float VOXEL_SCALE = 1.0 / 256.0;
    vec3 world_voxel_pos    = (vec3(atlas_uvi) + 0.5) * VOXEL_SCALE;

    float noise     = compute_noise(world_voxel_pos);
    float weight    = noise - world_voxel_pos.y;
    uint voxel_type = get_voxel_type(weight);

    imageStore(raw_atlas, atlas_uvi, uvec4(voxel_type, 0, 0, 0));
}

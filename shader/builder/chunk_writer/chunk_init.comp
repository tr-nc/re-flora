#version 450
#extension GL_GOOGLE_include_directive : require

layout(local_size_x = 4, local_size_y = 4, local_size_z = 4) in;

layout(set = 0, binding = 0) readonly uniform U_RegionInfo {
    uvec3 offset;
    uvec3 dim;
}
region_info;

layout(set = 0, binding = 1, r8ui) writeonly uniform uimage3D chunk_atlas;

#include "../../include/config.glsl"
#include "../../include/core/fast_noise_lite.glsl"
#include "../../include/voxel_registry.glsl"

float compute_noise(vec3 p) {
    // https://auburn.github.io/FastNoiseLite/

    float noise_scale = 0.4;

    fnl_state state    = fnlCreateState(42);
    state.noise_type   = FNL_NOISE_PERLIN;
    state.fractal_type = FNL_FRACTAL_FBM;
    state.frequency    = 1.0;
    state.octaves      = 5;
    state.lacunarity   = 2.6;
    state.gain         = 0.2;
    // its return value is automatically
    float noise_01 = fnlGetNoise3D(state, p.x, p.y, p.z) * 0.5 + 0.5;
    return noise_01 * noise_scale;
}

uint get_voxel_type(float weight) {
    if (weight < 0.0) {
        return VOXEL_TYPE_EMPTY;
    }
    return VOXEL_TYPE_DIRT;
}

void main() {
    ivec3 uvi = ivec3(gl_GlobalInvocationID);
    if (any(greaterThanEqual(uvi, ivec3(region_info.dim)))) {
        return;
    }

    ivec3 atlas_uvi         = ivec3(region_info.offset + uvi);
    const float VOXEL_SCALE = 1.0 / 256.0;
    vec3 world_voxel_pos    = (vec3(atlas_uvi) + 0.5) * VOXEL_SCALE;

    float noise     = compute_noise(world_voxel_pos);
    float weight    = noise - world_voxel_pos.y;
    uint voxel_type = get_voxel_type(weight);

    imageStore(chunk_atlas, atlas_uvi, uvec4(voxel_type, 0, 0, 0));
}

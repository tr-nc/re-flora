#version 450

#extension GL_GOOGLE_include_directive : require

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;

layout(set = 0, binding = 0) readonly uniform U_ContreeBuildInfo {
    uint dim; // the dimension of the contree to be built, uniform in x, y, z
    uint max_level;
    uint node_write_offset; // the offset in the global node buffer
    uint leaf_write_offset; // the offset in the global leaf buffer
}
contree_build_info;

layout(set = 0, binding = 1) writeonly buffer B_ContreeBuildState {
    uint prev_dim;
    uint curr_dim;
    uint level;
}
contree_build_state;

layout(set = 0, binding = 2) writeonly buffer B_LevelDispatchIndirect {
    uint dispatch_x;
    uint dispatch_y;
    uint dispatch_z;
}
level_dispatch_indirect;

layout(set = 0, binding = 3) writeonly buffer B_CounterForLevels { uint data[]; }
counter_for_levels;

layout(set = 0, binding = 4) writeonly buffer B_NodeOffsetForLevels { uint data[]; }
node_offset_for_levels;

layout(set = 0, binding = 5) writeonly buffer B_ContreeBuildResult {
    uint node_len;
    uint leaf_len;
}
contree_build_result;

#include "../../include/config.glsl"
#include "../../include/core/dispatch_grouper.glsl"
#include "../../include/core/math.glsl"

void main() {
    uint prev_dim = contree_build_info.dim;
    uint curr_dim = prev_dim / 4;

    contree_build_state.prev_dim = prev_dim;
    contree_build_state.curr_dim = curr_dim;
    // when dim is guaranteed to be a power of 4, this method can find log4(dim)
    contree_build_state.level = log_4(curr_dim);

    uint dispatch_count                = group_x_4(curr_dim);
    level_dispatch_indirect.dispatch_x = dispatch_count;
    level_dispatch_indirect.dispatch_y = dispatch_count;
    level_dispatch_indirect.dispatch_z = dispatch_count;

    // we use max_level - 1 because the last level is the leaf level
    for (uint i = 0; i < contree_build_info.max_level - 1; i++) {
        counter_for_levels.data[i] = 0;
    }

    uint cur                = 0;
    uint size_mul_per_level = 64;
    uint size               = 1;
    for (uint i = 0; i < contree_build_info.max_level - 1; i++) {
        node_offset_for_levels.data[i] = cur;
        cur += size;
        size *= size_mul_per_level;
    }

    contree_build_result.node_len = 0;
    contree_build_result.leaf_len = 0;
}
